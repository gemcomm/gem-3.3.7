!-------------------------------------- LICENCE BEGIN ------------------------------------
!Environment Canada - Atmospheric Science and Technology License/Disclaimer, 
!                     version 3; Last Modified: May 7, 2008.
!This is free but copyrighted software; you can use/redistribute/modify it under the terms 
!of the Environment Canada - Atmospheric Science and Technology License/Disclaimer 
!version 3 or (at your option) any later version that should be found at: 
!http://collaboration.cmc.ec.gc.ca/science/rpn.comm/license.html 
!
!This software is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; 
!without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. 
!See the above mentioned License/Disclaimer for more details.
!You should have received a copy of the License/Disclaimer along with this software; 
!if not, you can write to: EC-RPN COMM Group, 2121 TransCanada, suite 500, Dorval (Quebec), 
!CANADA, H9P 1J3; or send e-mail to service.rpn@ec.gc.ca
!-------------------------------------- LICENCE END --------------------------------------
***s/r adw_main_1_wnd_settls_tr - Equivalent to adw_main_1_wnd_settls for TRAJECTORY 
*
#include "model_macros_f.h"
*
      subroutine adw_main_1_wnd_settls_tr ( F_um, F_vm, F_wm, F_nit, F_njt, F_nk )
*
#include "impnone.cdk"
*
      integer F_nit, F_njt, F_nk
*
      real F_um(F_nit,F_njt,F_nk),
     %     F_vm(F_nit,F_njt,F_nk),
     %     F_wm(F_nit,F_njt,F_nk)
*
*author
*     monique tanguay
*
*revision
* v2_31 - Tanguay M.        - initial MPI version
* v3_00 - Tanguay M.        - adapt to restructured adw_main 
* v3_21 - Tanguay M.        - Revision Openmp
* v3_31 - Tanguay M.        - SETTLS option
* v3_35 - Tanguay M.        - SETTLS option a la Ritchie/Girard 
*
*language
*     fortran 77
*
*object
*     see id section
*
*TRAJECTORY of
*arguments
*______________________________________________________________________
*              |                                                 |     |
* NAME         | DESCRIPTION                                     | I/O |
*--------------|-------------------------------------------------|-----|
* F_u          | u components of winds on advection grid         |  o  |
* F_v          | v components of winds on advection grid         |  o  |
* F_w          | w components of winds on advection grid         |  o  |
*              |                                                 |     |
* F_nit        | \ total number of points in x,y direction in    |  i  |
* F_njt        | / advection grid (including halos)              |  i  |
*              |                                                 |     |
* F_nk         | number of levels                                |  i  |
*______________|_________________________________________________|_____|
*
*implicits
#include "glb_ld.cdk"
#include "glb_pil.cdk"
#include "adw.cdk"
#include "lun.cdk"
#include "inuvl.cdk"
#include "vthm.cdk"
#include "vt1m.cdk"
#include "vtwm.cdk"
*
*modules
      integer  vmmlod, vmmget, vmmuld
      external vmmlod, vmmget, vmmuld
************************************************************************
      integer pnerr, pnlkey1(30), pnlod
*
      integer i, j, k, i0, in, j0, jn, n, dest_ni
      real*8 ONE_8,sc_8
      parameter( ONE_8 = 1.0 )
*
      real um_10(LDIST_SHAPE,l_nk),vm_10(LDIST_SHAPE,l_nk),wm_10(LDIST_SHAPE,l_nk)
      real um_20(LDIST_SHAPE,l_nk),vm_20(LDIST_SHAPE,l_nk),wm_20(LDIST_SHAPE,l_nk)
*
************************************************************************
      if (Lun_debug_L) write (Lun_out,1000)
*
      pnlkey1(1)  = VMM_KEY(uthm)
      pnlkey1(2)  = VMM_KEY(vthm)
      pnlkey1(3)  = VMM_KEY(psdthm)
      pnlkey1(4)  = VMM_KEY(ut1m)
      pnlkey1(5)  = VMM_KEY(vt1m)
      pnlkey1(6)  = VMM_KEY(psdt1m)
      pnlkey1(7)  = VMM_KEY(utwm)
      pnlkey1(8)  = VMM_KEY(vtwm)
      pnlkey1(9)  = VMM_KEY(psdtwm)
*
      pnerr = vmmlod(pnlkey1,9)
*
      pnerr = VMM_GET_VAR(uthm)
      pnerr = VMM_GET_VAR(vthm)
      pnerr = VMM_GET_VAR(psdthm)
      pnerr = VMM_GET_VAR(ut1m)
      pnerr = VMM_GET_VAR(vt1m)
      pnerr = VMM_GET_VAR(psdt1m)
      pnerr = VMM_GET_VAR(utwm)
      pnerr = VMM_GET_VAR(vtwm)
      pnerr = VMM_GET_VAR(psdtwm)
*
*     TRAJECTORY of
*     ------------------------------------------------------------------------------------------
*     At the end of ADW_MAIN_1_WND_SETTLS:
*     ------------------------------------------------------------------------------------------
*     F_u,F_v_F_w  = 2*V(x,t1)-V(x,t2)     to be interpolated when evaluating upstream positions
*     uth,vth,psdth=   V(x,t1)         NOT to be interpolated when evaluating upstream positions
*     ------------------------------------------------------------------------------------------
*
*     ----------------------------
*     Set V_10 = 2*V(x,t1)-V(x,t2)
*     Set V_20 =   V(x,t1)
*     ----------------------------
*
!$omp parallel private(i0,in,j0,jn,sc_8)
*
!$omp do 
      do k=1,l_nk
      do j=l_miny,l_maxy
      do i=l_minx,l_maxx
         um_10(i,j,k) = 2.*  ut1m(i,j,k) -  utwm(i,j,k)
         vm_10(i,j,k) = 2.*  vt1m(i,j,k) -  vtwm(i,j,k)
         wm_10(i,j,k) = 2.*psdt1m(i,j,k)- psdtwm(i,j,k)
*
         um_20(i,j,k) =   ut1m(i,j,k)
         vm_20(i,j,k) =   vt1m(i,j,k)
         wm_20(i,j,k) = psdt1m(i,j,k)
      enddo
      enddo
      enddo
!$omp enddo 
*
***********************************************************************
* Interpolate advection winds to geopotential grid
* F_u and F_v are used as work space
***********************************************************************
!$omp single 
      call rpn_comm_xch_halo (um_10,LDIST_DIM,l_niu,l_nj, G_nk,
     %             G_halox,G_haloy,G_periodx,G_periody,l_ni,0)
      call rpn_comm_xch_halo (vm_10,LDIST_DIM,l_ni,l_njv,G_nk,
     %             G_halox,G_haloy,G_periodx,G_periody,l_ni,0)
*
      call rpn_comm_xch_halo (um_20,LDIST_DIM,l_niu,l_nj, G_nk,
     %             G_halox,G_haloy,G_periodx,G_periody,l_ni,0)
      call rpn_comm_xch_halo (vm_20,LDIST_DIM,l_ni,l_njv,G_nk,
     %             G_halox,G_haloy,G_periodx,G_periody,l_ni,0)
!$omp end single 
*
!$omp do
      do k=1,l_nk
*
         i0 = 1
         in = l_niu
         j0 = 1
         jn = l_nj
         if (G_lam) then
             if (l_west) i0 = 3
             if (l_east) in = l_niu - 1
         endif
         do j = j0, jn
         do i = i0, in
*
            F_um(i,j,k) = inuvl_wxux3_8(i,1) * um_10(i-2,j,k)
     %                  + inuvl_wxux3_8(i,2) * um_10(i-1,j,k)
     %                  + inuvl_wxux3_8(i,3) * um_10(i  ,j,k)
     %                  + inuvl_wxux3_8(i,4) * um_10(i+1,j,k)
*
            uthm(i,j,k) = inuvl_wxux3_8(i,1) * um_20(i-2,j,k)
     %                  + inuvl_wxux3_8(i,2) * um_20(i-1,j,k)
     %                  + inuvl_wxux3_8(i,3) * um_20(i  ,j,k)
     %                  + inuvl_wxux3_8(i,4) * um_20(i+1,j,k)
*
         enddo
         enddo
*
         i0 = 1
         in = l_ni
         jn = l_njv
         if (l_south) j0 = 3
         if (l_north) jn = l_njv - 1
         do j = j0, jn
         do i = i0, in
*
            F_vm(i,j,k) = inuvl_wyvy3_8(j,1) * vm_10(i,j-2,k)
     %                  + inuvl_wyvy3_8(j,2) * vm_10(i,j-1,k)
     %                  + inuvl_wyvy3_8(j,3) * vm_10(i,j  ,k)
     %                  + inuvl_wyvy3_8(j,4) * vm_10(i,j+1,k)
*
            vthm(i,j,k) = inuvl_wyvy3_8(j,1) * vm_20(i,j-2,k)
     %                  + inuvl_wyvy3_8(j,2) * vm_20(i,j-1,k)
     %                  + inuvl_wyvy3_8(j,3) * vm_20(i,j  ,k)
     %                  + inuvl_wyvy3_8(j,4) * vm_20(i,j+1,k)
*
         enddo
         enddo
         if (.not.G_lam) then
         if (l_south) then
            do i = i0, in
*
            F_vm(i,j0-2,k) = inuvl_wyvy3_8(j0-2,3) * vm_10(i,j0-2,k)
     %                     + inuvl_wyvy3_8(j0-2,4) * vm_10(i,j0-1,k)
            F_vm(i,j0-1,k) = inuvl_wyvy3_8(j0-1,2) * vm_10(i,j0-2,k)
     %                     + inuvl_wyvy3_8(j0-1,3) * vm_10(i,j0-1,k)
     %                     + inuvl_wyvy3_8(j0-1,4) * vm_10(i,j0,k  )
*
            vthm(i,j0-2,k) = inuvl_wyvy3_8(j0-2,3) * vm_20(i,j0-2,k)
     %                     + inuvl_wyvy3_8(j0-2,4) * vm_20(i,j0-1,k)
            vthm(i,j0-1,k) = inuvl_wyvy3_8(j0-1,2) * vm_20(i,j0-2,k)
     %                     + inuvl_wyvy3_8(j0-1,3) * vm_20(i,j0-1,k)
     %                     + inuvl_wyvy3_8(j0-1,4) * vm_20(i,j0,k  )
*
            enddo
         endif
         if (l_north) then
            do i = i0, in
*
            F_vm(i,jn+2,k) = inuvl_wyvy3_8(jn+2,1) * vm_10(i,jn  ,k)
     %                     + inuvl_wyvy3_8(jn+2,2) * vm_10(i,jn+1,k)
            F_vm(i,jn+1,k) = inuvl_wyvy3_8(jn+1,1) * vm_10(i,jn-1,k)
     %                     + inuvl_wyvy3_8(jn+1,2) * vm_10(i,jn  ,k)
     %                     + inuvl_wyvy3_8(jn+1,3) * vm_10(i,jn+1,k)
*
            vthm(i,jn+2,k) = inuvl_wyvy3_8(jn+2,1) * vm_20(i,jn  ,k)
     %                     + inuvl_wyvy3_8(jn+2,2) * vm_20(i,jn+1,k)
            vthm(i,jn+1,k) = inuvl_wyvy3_8(jn+1,1) * vm_20(i,jn-1,k)
     %                     + inuvl_wyvy3_8(jn+1,2) * vm_20(i,jn  ,k)
     %                     + inuvl_wyvy3_8(jn+1,3) * vm_20(i,jn+1,k)
*
            enddo
         endif
         endif
      enddo
!$omp enddo
*
!$omp do
      do k=1,l_nk
      do j=l_miny,l_maxy
      do i=l_minx,l_maxx
         psdthm(i,j,k) = wm_20(i,j,k)
      enddo
      enddo
      enddo
!$omp enddo
*
***********************************************************************
* Image to component
***********************************************************************
      i0=1
      in=l_ni
      j0=1
      jn=l_nj
      if (G_lam) then
             if (l_south) j0 = 3
             if (l_north) jn = l_njv - 1
      endif
!$omp do
      do k = 1,l_nk
      do j = j0,jn
*
      sc_8 = ONE_8/Adw_cy_8(j)
*
      do i = i0,in
*
      vm_10(i,j,k) = sc_8 * F_vm(i,j,k)
*
      vthm (i,j,k) = sc_8 * vthm(i,j,k)
*
      enddo
      enddo
      enddo
!$omp enddo
      i0=1
      in=l_ni
      j0=1
      jn=l_nj
      if (G_lam) then
             if (l_west) i0 = 3
             if (l_east) in = l_niu - 1
      endif
!$omp do 
      do k = 1,l_nk
      do j = j0,jn
*
      sc_8 = ONE_8/Adw_cy_8(j)
*
      do i = i0,in
*
      um_10(i,j,k) = sc_8 * F_um(i,j,k)
*
      uthm (i,j,k) = sc_8 * uthm(i,j,k)
*
      enddo
      enddo
      enddo
!$omp enddo
*
***********************************************************************
* Adjust wind fields to advection grid
***********************************************************************
      if (G_lam) then
          n=0
          dest_ni=l_ni
      else
          n=999
          dest_ni=G_ni
      endif
*
!$omp single
      call rpn_comm_xch_halox ( um_10, LDIST_DIM, l_ni, l_nj, l_nk, 
     %  Adw_halox, Adw_haloy, G_periodx, G_periody, F_um, 1-Adw_halox, 
     %  Adw_nic+Adw_halox, 1-Adw_haloy, Adw_njc+Adw_haloy, dest_ni, n)
*
      call rpn_comm_xch_halox ( vm_10, LDIST_DIM, l_ni, l_nj, l_nk, 
     %  Adw_halox, Adw_haloy, G_periodx, G_periody, F_vm, 1-Adw_halox, 
     %  Adw_nic+Adw_halox, 1-Adw_haloy, Adw_njc+Adw_haloy, dest_ni, n)
*
      call rpn_comm_xch_halox ( wm_10, LDIST_DIM, l_ni, l_nj, l_nk, 
     %  Adw_halox, Adw_haloy, G_periodx, G_periody, F_wm, 1-Adw_halox, 
     %  Adw_nic+Adw_halox, 1-Adw_haloy, Adw_njc+Adw_haloy, dest_ni, n)
!$omp end single

*
      if (.not.G_lam) then
*
         if ( l_south ) then
*
            call adw_polw (F_um,F_vm,Adw_cx_8,Adw_sx_8,Adw_wx_8,
     %        Adw_sy_8,0,Adw_nic,Adw_halox,Adw_njc,Adw_haloy,l_nk)
            call adw_pols (F_wm,Adw_wx_8,0,Adw_nic,Adw_halox,Adw_njc,
     %        Adw_haloy,l_nk)
         endif
*
         if ( l_north ) then
*
            call adw_polw (F_um,F_vm,Adw_cx_8,Adw_sx_8,Adw_wx_8,
     %        Adw_sy_8,Adw_njc+1,Adw_nic,Adw_halox,Adw_njc,Adw_haloy,l_nk)
            call adw_pols (F_wm,Adw_wx_8,Adw_njc+1,Adw_nic,Adw_halox,
     %        Adw_njc,Adw_haloy,l_nk)
         endif

      endif
!$omp end parallel
*
 1000 format(3X,'TRAJ of PREPARE WINDS: (S/R ADW_MAIN_1_WND_SETTLS_TR)')
      return
      end
